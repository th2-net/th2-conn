plugins {
    id 'com.google.protobuf' version '0.8.8' apply false
    id 'com.palantir.docker' version '0.25.0' apply false
    id "de.undercouch.download" version "4.0.4" apply false
    id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply false
    id "com.jfrog.artifactory" version '4.15.1'
}

allprojects {
    apply plugin: 'base'
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'

    ext {
        sharedDir                       = file("${project.rootDir}/shared")
    }

    group = 'com.exactpro.th2'
    version = release_version

    repositories {
        maven {
            name 'MavenLocal'
            url sharedDir
        }
        mavenCentral()

        maven {
            name 'Bintray'
            url 'https://dl.bintray.com/exactpro/th2'
        }
        maven {
            name 'Bintray'
            url 'https://dl.bintray.com/exactpro/sailfish'
        }
        mavenLocal()


        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    dependencies {
        api platform('com.exactpro.th2:bom:2.7.+')
    }


    jar {
        manifest {
            attributes(
                    'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                    'Specification-Title':      '',
                    'Specification-Vendor':     'Exactpro Systems LLC',
                    'Implementation-Title':     project.archivesBaseName,
                    'Implementation-Vendor':    'Exactpro Systems LLC',
                    'Implementation-Vendor-Id': 'com.exactpro',
                    'Implementation-Version':   project.version
            )
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                name = 'localRepo'
                url = sharedDir
            }
        }
    }

}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.palantir.docker'
apply plugin: 'java-library'

ext {
    sailfishVersion                 = '3.2'
}

sourceCompatibility = 1.11
targetCompatibility = 1.11

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

dependencies {
    implementation "com.exactpro.th2:common"
    implementation "com.exactpro.th2:sailfish-utils"
    implementation "com.exactpro.th2:grpc-common"
//    implementation "com.exactpro.cradle:cradle-core"

    implementation "org.slf4j:slf4j-log4j12"
    implementation "org.slf4j:slf4j-api"

    implementation 'com.google.protobuf:protobuf-java-util'
    implementation 'io.grpc:grpc-stub'

    implementation group: 'io.reactivex.rxjava3', name: 'rxjava', version: '3.0.4'

    implementation 'io.prometheus:simpleclient'

    implementation "com.exactpro.sf:sailfish-core:${sailfishVersion}"

    testImplementation 'junit:junit:4.12'
}

application {
    mainClassName 'com.exactpro.th2.conn.MicroserviceMain'
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}
